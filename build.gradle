buildscript {
  ext {
    indraVersion = "1.2.0"
  }
}

plugins {
  id "net.kyori.indra" version "$indraVersion" apply false
  id "net.kyori.indra.checkstyle" version "$indraVersion" apply false
  id "net.kyori.indra.license-header" version "$indraVersion" apply false
  id "net.kyori.indra.publishing" version "$indraVersion" apply false
  id "net.kyori.indra.publishing.sonatype" version "$indraVersion" apply false

  id "com.adarshr.test-logger" version "2.1.1" apply false
}

group "net.kyori"
version "4.2.0-SNAPSHOT"
description "A user-interface library for Minecraft: Java Edition."

subprojects {
  apply plugin: "net.kyori.indra"
  apply plugin: "net.kyori.indra.checkstyle"
  apply plugin: "net.kyori.indra.license-header"
  apply plugin: "net.kyori.indra.publishing"
  apply plugin: "net.kyori.indra.publishing.sonatype"
  apply plugin: "com.adarshr.test-logger"

  indra {
    github("KyoriPowered", "adventure")
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }

          developer {
            id = "lucko"
            name = "Luck"
            url = "https://lucko.me"
            email = "git@lucko.me"
          }

          developer {
            id = "zml"
            name = "zml"
            timezone = "America/Vancouver"
          }

          developer {
            id = "Electroid"
          }
        }
      }
    }
  }

  testlogger {
    theme("mocha-parallel")
    showPassed(false)
  }

  // TODO: indra?
  tasks.withType(Sign) {
    onlyIf {
      (project.hasProperty("forceSign") || gradle.taskGraph.hasTask(':publish')) && !project.version.endsWith('-SNAPSHOT')
    }
  }

  configurations.testCompileClasspath {
    exclude group: "junit" // brought in by google's libs
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    checkstyle("ca.stellardrift:stylecheck:0.1")
    testImplementation("com.google.guava:guava-testlib:30.0-jre")
    testImplementation("com.google.truth:truth:1.1")
    testImplementation("com.google.truth.extensions:truth-java8-extension:1.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.7.0")
  }
}

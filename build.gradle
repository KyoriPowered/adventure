plugins {
  id 'net.minecrell.licenser' version "0.4.1" apply false
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven'
  apply plugin: 'signing'
  apply plugin: 'net.minecrell.licenser'
  apply plugin: 'checkstyle'

  group 'net.kyori'
  version '4.0.0-SNAPSHOT'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-parameters']
    options.deprecation = true
    options.encoding = 'UTF-8'
  }

  javadoc {
    options.encoding = 'UTF-8'
  }

  test {
    useJUnitPlatform()
  }

  license {
    header rootProject.file('header.txt')
    include '**/*.java'
    newLine false
  }

  checkstyle {
    def configRoot = new File(rootProject.projectDir, ".checkstyle")
    toolVersion = "8.34"
    configDirectory = configRoot
    configProperties= [basedir: configRoot.getAbsolutePath()]
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url 'https://oss.sonatype.org/content/groups/public/'
    }
  }

  dependencies {
    checkstyle 'ca.stellardrift:stylecheck:0.1-SNAPSHOT'
    testImplementation 'com.google.guava:guava-testlib:29.0-jre'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
  }

  task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives javadocJar
    archives sourcesJar
  }

  signing {
    required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') && project.hasProperty('signing.keyId') }
    sign configurations.archives
  }

  if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    uploadArchives {
      enabled = System.getenv('TRAVIS') == null || project.version.endsWith('-SNAPSHOT')

      repositories {
        mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
          }

          snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
          }

          pom.project {
            description 'A text library for Minecraft.'
            name project.name
            url 'https://github.com/KyoriPowered/text'

            developers {
              developer {
                id 'kashike'
                name 'kashike'
                timezone 'America/Vancouver'
              }

              developer {
                id 'lucko'
                name 'Luck'
                url 'https://lucko.me'
                email 'git@lucko.me'
              }
            }

            issueManagement {
              system 'GitHub Issues'
              url 'https://github.com/KyoriPowered/text/issues'
            }

            licenses {
              license {
                name 'MIT License'
                url 'https://opensource.org/licenses/MIT'
              }
            }

            scm {
              connection 'scm:git@github.com:KyoriPowered/text.git'
              developerConnection 'scm:git@github.com:KyoriPowered/text.git'
              url 'https://github.com/KyoriPowered/text'
            }
          }
        }
      }
    }
  }
}

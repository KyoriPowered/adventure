buildscript {
  ext {
    indraVersion = "2.0.4"
  }
}

plugins {
  id "net.kyori.indra" version "$indraVersion" apply false
  id "net.kyori.indra.checkstyle" version "$indraVersion" apply false
  id "net.kyori.indra.license-header" version "$indraVersion" apply false
  id "net.kyori.indra.publishing.sonatype" version "$indraVersion"

  id "com.adarshr.test-logger" version "3.0.0" apply false
}

group "net.kyori"
version "4.8.0-SNAPSHOT"
description "A user-interface library for Minecraft: Java Edition."

def applyJarMetadata(project, moduleName) {
  if("jar" in project.tasks.names) {
    project.tasks.named("jar", Jar).configure {
      manifest.attributes(
        "Automatic-Module-Name": moduleName,
        "Specification-Title": moduleName,
        "Specification-Version": project.version,
        "Specification-Vendor": "KyoriPowered"
      )
      if(project.indraGit) {
        project.indraGit.applyVcsInformationToManifest(manifest)
      }
    }
  }
}

subprojects {
  // The bom is special... this is a bit ugly, but not much more we can do without making our own script plugin
  if(name != "adventure-bom") {
    apply plugin: "net.kyori.indra"
    apply plugin: "net.kyori.indra.checkstyle"
    apply plugin: "net.kyori.indra.license-header"
    apply plugin: "com.adarshr.test-logger"

    testlogger {
      theme("mocha-parallel")
      showPassed(false)
    }

    configurations.testCompileClasspath {
      exclude group: "junit" // brought in by google's libs
    }

    repositories {
      mavenLocal()
      mavenCentral()
    }

    dependencies {
      api(platform(projects.adventureBom))
      checkstyle(libs.stylecheck)
      testImplementation(libs.google.guava)
      testImplementation(libs.google.guavaTest)
      testImplementation(libs.google.truth)
      testImplementation(libs.google.truthJava8)
      testImplementation(libs.junit.api)
      testImplementation(libs.junit.params)
      testRuntimeOnly(libs.junit.engine)
    }

    javadoc {
      options.tags(["sinceMinecraft:a:Since Minecraft:"])
    }
  }

  // All projects
  apply plugin: "net.kyori.indra.publishing"

  indra {
    javaVersions {
      testWith(8, 11, 16)
    }

    github("KyoriPowered", "adventure") {
      ci(true)
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }

          developer {
            id = "lucko"
            name = "Luck"
            url = "https://lucko.me"
            email = "git@lucko.me"
          }

          developer {
            id = "zml"
            name = "zml"
            timezone = "America/Vancouver"
          }

          developer {
            id = "Electroid"
          }
        }
      }
    }
  }

}
